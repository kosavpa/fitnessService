import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.bmuschko.docker-remote-api' version '9.4.0'
}

group = 'com.owl'
version = ''

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.1")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
    implementation('org.springframework.cloud:spring-cloud-starter-netflix-eureka-server') {
        exclude(group: 'org.springframework.cloud', module: 'spring-cloud-starter-ribbon')
        exclude(group: 'com.netflix.ribbon', module: 'ribbon-eureka')
    }
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
    destinationDirectory = file("${project.buildDir}/docker/")
}

task createDockerfile(type: Dockerfile, dependsOn: bootJar) {
    destFile = project.file('build/docker/Dockerfile')

    from 'openjdk:17-alpine'
    copyFile "${rootProject.name}.jar", "/app/${rootProject.name}.jar"
    entryPoint 'java'
    defaultCommand '-jar', "/app/${rootProject.name}.jar"
    exposePort 8080
}

task buildImage(type: DockerBuildImage, dependsOn: createDockerfile) {
    inputDir = file("build/docker/")

    getImages().add("kosavpa/${rootProject.name}:latest")
}

task createDockerfileAndBuildImage(dependsOn: [bootJar, createDockerfile, buildImage])